buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        flatDir { dirs 'dexguard/lib' }
    }

    dependencies {
        //noinspection GradleDynamicVersion
        classpath 'io.fabric.tools:gradle:1.+'
        classpath ':dexguard:'
    }
}
apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'dexguard'
apply plugin: 'me.tatarka.retrolambda'

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
}


android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        applicationId "com.passwordboss.android"
        minSdkVersion 15
        targetSdkVersion 23
        //noinspection AccidentalOctal, Bamboo's task changes it with build number
        versionCode 00000
        versionName '2.0.00000'
        multiDexEnabled true
    }

    signingConfigs {
        qa
        qa.storeFile = file('qa.keystore')
        qa.storePassword = 'wYaxZ~xQ(L4JYM-:'
        qa.keyAlias = 'pb'
        qa.keyPassword = 'wYaxZ~xQ(L4JYM-:'
    }

    buildTypes {
        qa {
            signingConfig signingConfigs.qa
        }

        release {
            proguardFile getDefaultDexGuardFile('dexguard-release.pro')
            proguardFile 'dexguard/dexguard-project.txt'
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
    }

    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    productFlavors {
        //noinspection GroovyMissingReturnStatement
        dev {
        }
        dev21 {
            // dev utilizes minSDKVersion = 21 to allow the Android gradle plugin
            // to pre-dex each module and produce an APK that can be tested on
            // Android Lollipop without time consuming dex merging processes.
            minSdkVersion 21
        }
        //noinspection GroovyMissingReturnStatement
        staging {
        }
        //noinspection GroovyMissingReturnStatement
        production {
        }

    }
    sourceSets {
        dev21 {
            // used 'dev' data
            manifest.srcFile 'src/dev/AndroidManifest.xml'
            java.srcDirs = ['src/dev/java']
            res.srcDirs = ['src/dev/res']

        }
        staging {
            // used 'dev' data
            manifest.srcFile 'src/dev/AndroidManifest.xml'
            java.srcDirs = ['src/dev/java']
            res.srcDirs = ['src/dev/res']
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def apk = output.outputFile;
            def newName = "${variant.mergedFlavor.versionName}-(${variant.flavorName}-${variant.buildType.name})-${variant.mergedFlavor.applicationId}"
            if (output.zipAlign) {
                output.zipAlign.outputFile = new File((File) apk.parentFile, newName + '-aligned.apk');
            }
            output.packageApplication.outputFile = new File((File) apk.parentFile, newName + ".apk")
        }
    }

}

dependencies {
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.google.guava:guava:18.0'
    compile 'joda-time:joda-time:2.4'
    compile files('libs/ShortcutBadgermain.jar')
    compile files('libs/androidsvg-1.2.0.jar')
    compile files('libs/core.jar')
    compile files('libs/js.jar')
    compile files('libs/okhttp-2.3.0.jar')
    compile files('libs/okio-1.3.0.jar')
    compile files('libs/ormlite-android-4.48.jar')
    compile files('libs/ormlite-android-chiper-orm-4.48.jar')
    compile files('libs/ormlite-core-4.48.jar')
    compile files('libs/passwordboss-pemhelper-bc-shrinked.jar')
    compile files('libs/segment-io-core-2.5.3.jar')
    compile files('libs/tape-1.2.3.jar')

    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'uk.co.chrisjenx:calligraphy:2.2.0'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.azimolabs.keyboardwatcher:keyboardwatcher:0.1.2'
    compile 'com.basgeekball:awesome-validation:1.1'
    compile 'eu.inmite.android.lib:android-validation-komensky:0.9.4@aar'

    compile 'com.github.traex:RippleEffect:df5f9e4456'
    compile 'com.github.bumptech.glide:glide:3.7.0'

    compile 'net.zetetic:android-database-sqlcipher:3.4.0'

    //noinspection GradleDynamicVersion
    compile 'com.instabug.library:instabugcompat:2+'

    /* Logging */
    compile 'com.github.tony19:logback-android-core:1.1.1-4'
    compile 'com.github.tony19:logback-android-classic:1.1.1-4'
    compile 'org.slf4j:slf4j-api:1.7.6'
    compile "com.daimajia.swipelayout:library:1.2.0@aar"
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }

    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:cardview-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'
    compile 'com.android.support:multidex:1.0.1'

    //Reactive programming
    compile 'io.reactivex:rxjava:1.1.5'
    compile 'io.reactivex:rxandroid:1.2.0'

    compile 'com.tonicartos:superslim:0.4.13'
}
